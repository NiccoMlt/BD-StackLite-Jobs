import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'it.unibo.bd1819'
version '1.0.0'

// Versions enforced by Unversity environment
def javaVersion = JavaVersion.VERSION_1_7
def scalaBinaryVersion = "2.11"
def scalaMinorVersion = "8"
def scalaVersion = "$scalaBinaryVersion.$scalaMinorVersion"
def clouderaHadoopSuffix = "cdh5.13.1"
def clouderaHadoopCoreSuffix = "mr1-$clouderaHadoopSuffix"
def clouderaSparkSuffix = "cloudera2"
def hadoopStandardVersion = "2.6.0"
def sparkStandardVersion = "2.1.0"
def hadoopVersion = "$hadoopStandardVersion-$clouderaHadoopSuffix"
def hadoopCoreVersion = "$hadoopStandardVersion-$clouderaHadoopCoreSuffix"
def sparkVersion = "$sparkStandardVersion.$clouderaSparkSuffix"

java {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://repository.cloudera.com/artifactory/cloudera-repos" }
}

dependencies {
    shadow "org.scala-lang:scala-library:$scalaVersion"
    shadow "org.scala-lang:scala-reflect:$scalaVersion"
    shadow "org.scala-lang:scala-compiler:$scalaVersion"

    testImplementation "junit:junit:$jUnit4Version"
    testImplementation "org.scalatest:scalatest_$scalaBinaryVersion:$scalaTestVersion"

    testRuntimeOnly "org.scala-lang.modules:scala-xml_$scalaBinaryVersion:$scalaXmlVersion"

    implementation 'joda-time:joda-time:2.10.3'
    implementation 'de.jollyday:jollyday:0.4.9'

    shadow "org.apache.spark:spark-mllib_$scalaBinaryVersion:$sparkStandardVersion"
    shadow "org.apache.spark:spark-sql_$scalaBinaryVersion:$sparkVersion"
    shadow "org.apache.spark:spark-streaming-flume-assembly_$scalaBinaryVersion:$sparkStandardVersion"
    shadow "org.apache.spark:spark-graphx_$scalaBinaryVersion:$sparkVersion"
    shadow "org.apache.spark:spark-launcher_$scalaBinaryVersion:$sparkVersion"
    shadow "org.apache.spark:spark-catalyst_$scalaBinaryVersion:$sparkVersion"
    shadow "org.apache.spark:spark-streaming_$scalaBinaryVersion:$sparkVersion"
    shadow "org.apache.spark:spark-core_$scalaBinaryVersion:$sparkVersion"

    shadow "org.apache.hadoop:hadoop-common:$hadoopVersion"
    shadow "org.apache.hadoop:hadoop-core:$hadoopCoreVersion"
    shadow "org.apache.hadoop:hadoop-hdfs:$hadoopVersion"

    
    compile group: 'org.rogach', name: "scallop_$scallopBinaryVersion", version: "$scallopVersion"
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

shadowJar {
    zip64 true
}

task mapReduceJar(type: ShadowJar) {
    //noinspection GroovyAssignabilityCheck
    archiveClassifier.set('mr')
    manifest.attributes 'Main-Class': 'it.unibo.bd1819.DummyMain'
    from(shadowJar.from())
    zip64 true
}

task sparkJar(type: ShadowJar) {
    //noinspection GroovyAssignabilityCheck
    archiveClassifier.set('spark')
    manifest.attributes 'Main-Class': 'it.unibo.bd1819.ScalaMain'
    from(shadowJar.from())
    zip64 true
}


defaultTasks 'clean', 'build', 'javadoc', 'scaladoc'
