import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    ext {
        shadow_version = '5.1.0'
    }
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:${shadow_version}"
    }
}

// Versions enforced by University server environment
ext {
    java_version = JavaVersion.VERSION_1_7
    scala_binary_version = "2.11"
    scala_minor_version = "8"
    scala_version = "$scala_binary_version.$scala_minor_version"
    cloudera_hadoop_suffix = "cdh5.13.1"
    cloudera_hadoop_core_suffix = "mr1-$cloudera_hadoop_suffix"
    cloudera_spark_suffix = "cloudera2"
    hadoop_standard_version = "2.6.0"
    spark_standard_version = "2.1.0"
    hadoop_version = "$hadoop_standard_version-$cloudera_hadoop_suffix"
    hadoop_core_version = "$hadoop_standard_version-$cloudera_hadoop_core_suffix"
    spark_version = "$spark_standard_version.$cloudera_spark_suffix"
    slf4j_version = "1.7.5"

    jUnit4_version = "4.12"
    jetbrains_annotations_version = "17.0.0"
    scalaTest_version = "3.0.7"
    scalaXml_version = "1.2.0"
    scallop_version = "3.1.3"
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

group 'it.unibo.bd1819'
version '1.0.0'

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://repository.cloudera.com/artifactory/cloudera-repos" }
        mavenCentral()
        jcenter()
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        zip64 true
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
